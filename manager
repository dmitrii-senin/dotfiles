#! /bin/zsh

autoload -Uz colors && colors

prog_name=$(basename $0)
repo_root="${0:A:h}"

function _build_deploy_paths() {
	local dest_root=${1:-$HOME}

	declare -gA deploy_paths
	deploy_paths=()

	local prefix_black_list=(.git/ README.md LICENSE manager)
	for repo_path in $(find "$repo_root" -type f ); do
		local rel_path="${repo_path##"${repo_root}"/}"
		local in_black_list="no"

		# check all black list prefixes
		for prefix in $prefix_black_list; do
			if [[ "$rel_path" =~ "^${prefix}*" ]]; then
				local in_black_list="yes"
				break
			fi
		done

		if [ $in_black_list = "no" ]; then
			# if we didn't find this path in black list
			local dest_path="$dest_root/$rel_path"
			deploy_paths[$repo_path]="$dest_path"
		fi
	done
}

function subcmd_help() {
	echo "Usage: ${fg_bold[white]}${prog_name}${reset_color} <subcommand> [options]"
	echo
	echo "SUBCOMMANDS"
	echo "    ${fg_bold[white]}pull${reset_color}      Pull the last dotfiles repo changes"
	echo "    ${fg_bold[white]}deploy${reset_color}   Copy dotfiles to destination directories"
	echo "    ${fg_bold[white]}sync${reset_color}      Sync deployed dotfiles with the repo"
	echo "    ${fg_bold[white]}push${reset_color}      Add and push local dotfiles changes to the repo"
	echo
	echo "DETAILS"
	echo "${fg_bold[white]}deploy${reset_color} <dest-dir>"
	echo "   ${fg_bold[white]}dest-dir${reset_color}"
	echo "        Directory to where the dotfiles will be deployed (default: \$HOME)."
}

function subcmd_pull() {
	git -C "$repo_root" pull -q > /dev/null 2>&1
	if [ $? -ne 0 ]; then echo "Error: Failed to pull dotfiles repo" && return 1; fi
	remote_url=$(git -C "$repo_root" remote -v | grep fetch | grep -Eo "[[:space:]].*[[:space:]]" | tr -d '[:space:]')
	echo "Successfull git pull from $remote_url"
}

function subcmd_deploy() {
	local dest_dir=${1:-$HOME}
	_build_deploy_paths "$dest_dir"
	for repo_path dest_path in ${(kv)deploy_paths}; do
		echo "'$repo_path' => '$dest_path'"
		mkdir -p $(dirname "$dest_path") && cp "$repo_path" "$dest_path"
	done
}

function subcmd_sync() {
	local deploy_dir=${1:-$HOME}
	_build_deploy_paths "$deploy_dir"
	for repo_path deploy_path in ${(kv)deploy_paths}; do
		echo "'$deploy_path' => '$repo_path'"
		[ -f "$deploy_path" ] && cp "$deploy_path" "$repo_path"
	done
}

subcommand=$1
case $subcommand in
	"" | "-h" | "--help")
		subcmd_help
		;;

	*)
		shift
		subcmd_${subcommand} $@
		if [ $? = 127 ]; then
			echo "Error: '$subcommand' is not a known subcommand." >&2
			echo "Please run '$prog_name --help' to get a list of supported subcommands." >&2
			exit 1
		fi
		;;
esac
